apply plugin: "signing"
apply plugin: "maven-publish"

String repoUsername = properties["repoUsername"]
String repoPassword = properties["repoPassword"]

publishing.publications.withType(MavenPublication.class) {
  pom {
    afterEvaluate {
      name = project.name
      description = project.description
    }
    url = "https://github.com/TAKETODAY/polaris"
    organization {
      name = "TODAY Tech"
      url = "https://taketoday.cn"
    }
    licenses {
      license {
        name = "Apache License, Version 2.0"
        url = "https://www.apache.org/licenses/LICENSE-2.0"
        distribution = "repo"
      }
    }
    scm {
      url = "https://github.com/TAKETODAY/polaris.git"
      connection = "scm:git:git://github.com/TAKETODAY/polaris"
      developerConnection = "scm:git:git://github.com/TAKETODAY/polaris"
    }
    developers {
      developer {
        id = "taketoday"
        name = "海子 Yang"
        email = "i.take.today@gmail.com"
      }
    }
    issueManagement {
      system = "GitHub"
      url = "https://github.com/TAKETODAY/polaris/issues"
    }
  }
  versionMapping {
    usage('java-api') {
      fromResolutionResult()
    }
    usage('java-runtime') {
      fromResolutionResult()
    }
  }
}

// Disable Gradle module metadata publishing until we know what we want.
// https://docs.gradle.org/8.3/userguide/publishing_gradle_module_metadata.html#sub:disabling-gmm-publication
tasks.withType(GenerateModuleMetadata).configureEach {
  enabled = false
}

publishing {
  repositories {
    maven {
      url = isSnapshot
          ? 'https://oss.sonatype.org/content/repositories/snapshots/'
          : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'

      credentials {
        username = repoUsername
        password = repoPassword
      }
    }
  }
}
